-- TB EXAMPLE PFRL 2023-2024

library ieee;
use ieee.std_logic_1164.all;
use ieee.numeric_std.all;
use std.textio.all;

entity project_tb is
end project_tb;

architecture project_tb_arch of project_tb is
    constant CLOCK_PERIOD : time := 20 ns;
    signal tb_clk : std_logic := '0';
    signal tb_rst, tb_start, tb_done : std_logic;
    signal tb_add : std_logic_vector(15 downto 0);
    signal tb_k   : std_logic_vector(9 downto 0);

    signal tb_o_mem_addr, exc_o_mem_addr, init_o_mem_addr : std_logic_vector(15 downto 0);
    signal tb_o_mem_data, exc_o_mem_data, init_o_mem_data : std_logic_vector(7 downto 0);
    signal tb_i_mem_data : std_logic_vector(7 downto 0);
    signal tb_o_mem_we, tb_o_mem_en, exc_o_mem_we, exc_o_mem_en, init_o_mem_we, init_o_mem_en : std_logic;

    type ram_type is array (65535 downto 0) of std_logic_vector(7 downto 0);
    signal RAM : ram_type := (OTHERS => "00000000");

constant SCENARIO_LENGTH : integer := 185;
    type scenario_type is array (0 to SCENARIO_LENGTH*2-1) of integer;

signal scenario_input : scenario_type := (0,0,23,0,0,0,165,0,0,0,184,0,22,0,61,0,239,0,230,0,185,0,5,0,0,0,58,0,12,0,142,0,33,0,18,0,231,0,116,0,145,0,183,0,164,0,0,0,0,0,189,0,90,0,2,0,104,0,0,0,142,0,0,0,0,0,122,0,111,0,38,0,40,0,59,0,0,0,134,0,21,0,214,0,111,0,225,0,148,0,171,0,154,0,0,0,181,0,218,0,139,0,168,0,213,0,247,0,81,0,37,0,124,0,190,0,24,0,62,0,0,0,164,0,208,0,0,0,151,0,0,0,252,0,0,0,195,0,203,0,23,0,170,0,189,0,232,0,41,0,38,0,243,0,34,0,0,0,155,0,0,0,0,0,0,0,205,0,77,0,157,0,116,0,75,0,228,0,79,0,150,0,0,0,39,0,102,0,237,0,22,0,247,0,0,0,7,0,42,0,243,0,97,0,255,0,0,0,0,0,157,0,4,0,249,0,139,0,206,0,241,0,23,0,254,0,37,0,40,0,219,0,95,0,33,0,84,0,162,0,59,0,52,0,116,0,137,0,235,0,176,0,178,0,148,0,0,0,161,0,72,0,151,0,0,0,121,0,147,0,110,0,0,0,116,0,0,0,218,0,224,0,34,0,40,0,199,0,0,0,102,0,0,0,56,0,221,0,159,0,0,0,0,0,236,0,182,0,52,0,115,0,119,0,223,0,0,0,160,0,49,0,253,0,206,0,167,0,0,0,200,0,0,0,128,0,0,0,199,0,68,0,216,0,40,0,39,0,137,0,180,0,236,0,74,0,0,0,0,0,124,0,234,0,247,0,153,0,149,0);
signal scenario_full  : scenario_type := (0,0,23,31,23,30,165,31,165,30,184,31,22,31,61,31,239,31,230,31,185,31,5,31,5,30,58,31,12,31,142,31,33,31,18,31,231,31,116,31,145,31,183,31,164,31,164,30,164,29,189,31,90,31,2,31,104,31,104,30,142,31,142,30,142,29,122,31,111,31,38,31,40,31,59,31,59,30,134,31,21,31,214,31,111,31,225,31,148,31,171,31,154,31,154,30,181,31,218,31,139,31,168,31,213,31,247,31,81,31,37,31,124,31,190,31,24,31,62,31,62,30,164,31,208,31,208,30,151,31,151,30,252,31,252,30,195,31,203,31,23,31,170,31,189,31,232,31,41,31,38,31,243,31,34,31,34,30,155,31,155,30,155,29,155,28,205,31,77,31,157,31,116,31,75,31,228,31,79,31,150,31,150,30,39,31,102,31,237,31,22,31,247,31,247,30,7,31,42,31,243,31,97,31,255,31,255,30,255,29,157,31,4,31,249,31,139,31,206,31,241,31,23,31,254,31,37,31,40,31,219,31,95,31,33,31,84,31,162,31,59,31,52,31,116,31,137,31,235,31,176,31,178,31,148,31,148,30,161,31,72,31,151,31,151,30,121,31,147,31,110,31,110,30,116,31,116,30,218,31,224,31,34,31,40,31,199,31,199,30,102,31,102,30,56,31,221,31,159,31,159,30,159,29,236,31,182,31,52,31,115,31,119,31,223,31,223,30,160,31,49,31,253,31,206,31,167,31,167,30,200,31,200,30,128,31,128,30,199,31,68,31,216,31,40,31,39,31,137,31,180,31,236,31,74,31,74,30,74,29,124,31,234,31,247,31,153,31,149,31);

    signal memory_control : std_logic := '0';
    
    constant SCENARIO_ADDRESS : integer := 1234;

    component project_reti_logiche is
        port (
                i_clk : in std_logic;
                i_rst : in std_logic;
                i_start : in std_logic;
                i_add : in std_logic_vector(15 downto 0);
                i_k   : in std_logic_vector(9 downto 0);
                
                o_done : out std_logic;
                
                o_mem_addr : out std_logic_vector(15 downto 0);
                i_mem_data : in  std_logic_vector(7 downto 0);
                o_mem_data : out std_logic_vector(7 downto 0);
                o_mem_we   : out std_logic;
                o_mem_en   : out std_logic
        );
    end component project_reti_logiche;

begin
    UUT : project_reti_logiche
    port map(
                i_clk   => tb_clk,
                i_rst   => tb_rst,
                i_start => tb_start,
                i_add   => tb_add,
                i_k     => tb_k,
                
                o_done => tb_done,
                
                o_mem_addr => exc_o_mem_addr,
                i_mem_data => tb_i_mem_data,
                o_mem_data => exc_o_mem_data,
                o_mem_we   => exc_o_mem_we,
                o_mem_en   => exc_o_mem_en
    );

    -- Clock generation
    tb_clk <= not tb_clk after CLOCK_PERIOD/2;

    -- Process related to the memory
    MEM : process (tb_clk)
    begin
        if tb_clk'event and tb_clk = '1' then
            if tb_o_mem_en = '1' then
                if tb_o_mem_we = '1' then
                    RAM(to_integer(unsigned(tb_o_mem_addr))) <= tb_o_mem_data after 1 ns;
                    tb_i_mem_data <= tb_o_mem_data after 1 ns;
                else
                    tb_i_mem_data <= RAM(to_integer(unsigned(tb_o_mem_addr))) after 1 ns;
                end if;
            end if;
        end if;
    end process;
    
    memory_signal_swapper : process(memory_control, init_o_mem_addr, init_o_mem_data,
                                    init_o_mem_en,  init_o_mem_we,   exc_o_mem_addr,
                                    exc_o_mem_data, exc_o_mem_en, exc_o_mem_we)
    begin
        -- This is necessary for the testbench to work: we swap the memory
        -- signals from the component to the testbench when needed.
    
        tb_o_mem_addr <= init_o_mem_addr;
        tb_o_mem_data <= init_o_mem_data;
        tb_o_mem_en   <= init_o_mem_en;
        tb_o_mem_we   <= init_o_mem_we;

        if memory_control = '1' then
            tb_o_mem_addr <= exc_o_mem_addr;
            tb_o_mem_data <= exc_o_mem_data;
            tb_o_mem_en   <= exc_o_mem_en;
            tb_o_mem_we   <= exc_o_mem_we;
        end if;
    end process;
    
    -- This process provides the correct scenario on the signal controlled by the TB
    create_scenario : process
    begin
        wait for 50 ns;

        -- Signal initialization and reset of the component
        tb_start <= '0';
        tb_add <= (others=>'0');
        tb_k   <= (others=>'0');
        tb_rst <= '1';
        
        -- Wait some time for the component to reset...
        wait for 50 ns;
        
        tb_rst <= '0';
        memory_control <= '0';  -- Memory controlled by the testbench
        
        wait until falling_edge(tb_clk); -- Skew the testbench transitions with respect to the clock

        -- Configure the memory        
        for i in 0 to SCENARIO_LENGTH*2-1 loop
            init_o_mem_addr<= std_logic_vector(to_unsigned(SCENARIO_ADDRESS+i, 16));
            init_o_mem_data<= std_logic_vector(to_unsigned(scenario_input(i),8));
            init_o_mem_en  <= '1';
            init_o_mem_we  <= '1';
            wait until rising_edge(tb_clk);   
        end loop;
        
        wait until falling_edge(tb_clk);

        memory_control <= '1';  -- Memory controlled by the component
        
        tb_add <= std_logic_vector(to_unsigned(SCENARIO_ADDRESS, 16));
        tb_k   <= std_logic_vector(to_unsigned(SCENARIO_LENGTH, 10));
        
        tb_start <= '1';

        while tb_done /= '1' loop                
            wait until rising_edge(tb_clk);
        end loop;

        wait for 5 ns;
        
        tb_start <= '0';
        
        wait;
        
    end process;

    -- Process without sensitivity list designed to test the actual component.
    test_routine : process
    begin

        wait until tb_rst = '1';
        wait for 25 ns;
        assert tb_done = '0' report "TEST FALLITO o_done !=0 during reset" severity failure;
        wait until tb_rst = '0';

        wait until falling_edge(tb_clk);
        assert tb_done = '0' report "TEST FALLITO o_done !=0 after reset before start" severity failure;
        
        wait until rising_edge(tb_start);

        while tb_done /= '1' loop                
            wait until rising_edge(tb_clk);
        end loop;

        assert tb_o_mem_en = '0' or tb_o_mem_we = '0' report "TEST FALLITO o_mem_en !=0 memory should not be written after done." severity failure;

        for i in 0 to SCENARIO_LENGTH*2-1 loop
            assert RAM(SCENARIO_ADDRESS+i) = std_logic_vector(to_unsigned(scenario_full(i),8)) report "TEST FALLITO @ OFFSET=" & integer'image(i) & " expected= " & integer'image(scenario_full(i)) & " actual=" & integer'image(to_integer(unsigned(RAM(i)))) severity failure;
        end loop;

        wait until falling_edge(tb_start);
        assert tb_done = '1' report "TEST FALLITO o_done !=0 after reset before start" severity failure;
        wait until falling_edge(tb_done);

        assert false report "Simulation Ended! TEST PASSATO (EXAMPLE)" severity failure;
    end process;

end architecture;
